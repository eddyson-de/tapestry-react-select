plugins {
    id "com.jfrog.bintray" version "1.7.3"
    id "com.moowork.node" version "1.1.1"
}

description = "Use React-Select together with Tapestry"

group = "de.eddyson"
version = "0.2.6"

apply plugin: 'groovy'
apply plugin: 'maven'


def versions= [
  tapestry: '5.4.3',
  slf4j: '1.7.22',
  
  // test scopes
  spock: '1.1-groovy-2.4',
  selenium: '3.5.2'
  
]

repositories {
  jcenter()
}

sourceCompatibility = '1.8'

task sourceJar(type: Jar) {
  dependsOn classes
  classifier "sources"
  from sourceSets.main.allSource
}

artifacts {
  archives sourceJar
}

dependencies {
  compile "org.slf4j:slf4j-api:$versions.slf4j"
  compile "org.apache.tapestry:tapestry-core:$versions.tapestry"
  
  testCompile "javax.servlet:servlet-api:2.5"
  testCompile "org.spockframework:spock-tapestry:$versions.spock"
  testCompile 'de.eddyson:tapestry-geb:0.37.1'
  testCompile "org.seleniumhq.selenium:selenium-firefox-driver:$versions.selenium"
  testCompile "org.seleniumhq.selenium:selenium-chrome-driver:$versions.selenium"
  testCompile "io.github.bonigarcia:webdrivermanager:1.7.1"
  
  testRuntime "org.slf4j:slf4j-simple:$versions.slf4j"
  testRuntime 'de.eddyson:tapestry-react:0.30.2'
  testRuntime "org.apache.tapestry:tapestry-webresources:$versions.tapestry"
}

jar {
  manifest { attributes 'Tapestry-Module-Classes': 'de.eddyson.tapestry.react.select.ReactSelectModule' }
}

node.download = true

tasks.withType(Test){
  systemProperties["tapestry.service-reloading-enabled"] = "false"
  systemProperties["tapestry.execution-mode"] = "test"
  systemProperty 'webappLocation', 'src/test/webapp'
  systemProperty 'jettyPort', 9040
  enableAssertions = true
  testLogging {
    exceptionFormat "full"
  }
}

test {
  systemProperty "geb.env", System.getProperty("geb.env") ?: 'firefox'
}

task createBundles(type: NpmTask) {
  dependsOn npmInstall
  // install the express package only
  args = ['run', 'create-bundles']
}

processResources {
  dependsOn npmInstall, createBundles
  from('node_modules/react-select/dist'){
    into 'META-INF/assets/de/eddyson/tapestry/react/select'
    include '*.css'
  }
  from('rollup/dist'){
    into 'de/eddyson/tapestry/react/select'
  }
  from('node_modules/classnames'){
    into 'de/eddyson/tapestry/react/select'
    include 'index.js'
    rename { 'classnames.js' }
  }
}


task runTestApp(type:JavaExec) {
  main = 'org.apache.tapestry5.test.Jetty7Runner'
  args "-d", "src/test/webapp/", "-p", "9040"
  systemProperties["tapestry.execution-mode"] = "test"
  classpath = configurations.testRuntime + sourceSets.test.output + sourceSets.main.output
}

bintray {
    user = 'fooberger'
    key = System.env.bintrayAPIKey
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'de.eddyson:tapestry-react-select'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/eddyson-de/tapestry-react-select'
    }
}
